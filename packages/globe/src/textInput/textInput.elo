variant Validation {
  Invalid
}

variant Position {
  Start
  End
  Both
}

view Input {
  __WebkitAppearance: none
  __MozAppearance: none
  __appearance: none
  __outline: 0
  borderWidth: 2
  flexShrink: 1
  alignSelf: flexStart
  width: percentage(100)
  borderStyle: solid
  borderColor: rgb(220 220 220)
  borderRadius: 7
  paddingLeft: 15
  paddingRight: 15
  paddingTop: 11
  paddingBottom: 11
  [@hover] {
    borderColor: rgb(205 205 205)
  }
  [@focus] {
    __boxShadow: raw("0 0 6px rgba(0, 207, 192, 0.4)")
    borderColor: rgba(0 176 164 percentage(50))
  }
  [Validation=Invalid] {
    __boxShadow: raw("0 0 6px rgba(220, 60, 110, 0.4)")
    borderColor: rgba(209 50 90 percentage(50))
    backgroundColor: rgb(255 250 250)
    [@focus] {
      __boxShadow: raw("0 0 6px rgba(220, 60, 110, 0.4)")
      borderColor: rgba(209 50 90 percentage(50))
    }
    [@hover] {
      borderColor: rgba(209 50 90 percentage(50))
    }
  }
  [@disabled] {
    __cursor: notAllowed
    backgroundColor: rgb(245 245 245)
    [@hover] {
      borderColor: rgb(220 220 220)
    }
    [@focus] {
      __boxShadow: none
      borderColor: rgb(220 220 220)
    }
  }
  [Position=Start] {
    borderTopLeftRadius: 0
    borderBottomLeftRadius: 0
    paddingLeft: 4
  }
  [Position=End] {
    borderTopRightRadius: 0
    borderBottomRightRadius: 0
    paddingRight: 4
  }
  [Position=Both] {
    borderTopLeftRadius: 0
    borderBottomLeftRadius: 0
    borderTopRightRadius: 0
    borderBottomRightRadius: 0
    paddingLeft: 4
    paddingRight: 4
  }
}

text InputText {
  fontSize: 16
  lineHeight: 1.25
  color: rgb(20 20 20)
  [Position=End] {
    textAlign: right
  }
  [@disabled] {
    __WebkitTextFillColor: rgb(100 100 100)
    color: rgb(100 100 100)
  }
}

view Mask {
  backgroundColor: rgb(220 220 220)
  alignItems: center
  justifyContent: center
  paddingTop: 11
  paddingBottom: 11
  [Position=Start] {
    paddingLeft: 12
    paddingRight: 4
    borderTopLeftRadius: 7
    borderTopRightRadius: 0
    borderBottomRightRadius: 0
    borderBottomLeftRadius: 7
  }
  [Position=End] {
    paddingRight: 12
    paddingLeft: 4
    borderTopLeftRadius: 0
    borderTopRightRadius: 7
    borderBottomRightRadius: 7
    borderBottomLeftRadius: 0
  }
}

text MaskText {
  color: rgb(100 100 100)
  lineHeight: 1.2
  fontSize: 16
}
