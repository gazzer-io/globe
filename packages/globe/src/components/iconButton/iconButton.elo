variant Variant {
  Primary
  Destructive
}

view IconButton {
  __display: inlineFlex
  __WebkitUserSelect: none
  __MozUserSelect: none
  __userSelect: none
  __WebkitAppearance: none
  __MozAppearance: none
  __appearance: none
  __borderWidth: 0
  __cursor: pointer
  __transitionDuration: raw("150ms, 40ms")
  __transitionProperty: raw("background-color, transform")
  __transitionTimingFunction: easeInOut
  flexDirection: row
  width: 48
  height: 48
  alignItems: center
  justifyContent: center
  borderRadius: 48
  [Variant=Primary] {
    __boxShadow: raw("0 4px 12px rgba(0, 207, 192, 0.4)")
    backgroundColor: $theme_colors_primary
    [@hover] {
      backgroundColor: $theme_colors_primaryHover
    }
    [@active] {
      __boxShadow: raw("0 6px 20px rgba(0, 207, 192, 0.2)")
      __transform: raw("scale(0.99)")
      backgroundColor: $theme_colors_primaryActive
    }
    [@focus] {
      __boxShadow: raw("0 0 0 2pt rgb(60, 206, 194)")
    }
  }
  [Variant=Destructive] {
    __boxShadow: raw("0 4px 12px rgba(220, 60, 110, 0.4)")
    backgroundColor: $theme_colors_destructive
    [@hover] {
      backgroundColor: $theme_colors_destructiveHover
    }
    [@active] {
      __boxShadow: raw("0 6px 20px rgba(220, 60, 110, 0.2)")
      __transform: raw("scale(0.99)")
      backgroundColor: $theme_colors_destructiveActive
    }
    [@focus] {
      __boxShadow: raw("0 0 0 2pt rgb(229 80 120)")
    }
  }
  [@disabled] {
    __boxShadow: none
    __cursor: notAllowed
    backgroundColor: rgb(190 190 190)
    [@hover] {
      __boxShadow: none
      backgroundColor: rgb(190 190 190)
    }
    [@active] {
      __transform: none
      backgroundColor: rgb(190 190 190)
    }
  }
}

text IconButtonText {
  lineHeight: 1
  fontSize: 26
}
